buildscript {
    dependencies {
        classpath "org.liquibase:liquibase-core:5.0.1"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '3.0.2'
	id "org.jooq.jooq-codegen-gradle" version "3.20.10"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'backend'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
    mavenCentral()
}

ext {
    liquibaseVersion = '4.31.1'
    liquibaseHibernateVersion = '4.31.1'
    postgresVersion = '42.7.7'           

    // Liquibase settings
    diffVersion = project.hasProperty('diff.version') ? project.property('diff.version') : '0.1.0'
    diffChangelogFile = "src/main/resources/db/changelog/migration/_${diffVersion}_migrate.sql"
    changesetAuthor = project.hasProperty('author') ? project.property('author') : 'auto_generated'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation "org.liquibase:liquibase-core"
    runtimeOnly "org.postgresql:postgresql"

    // --- Dependencies required by the Liquibase Gradle plugin 
    liquibaseRuntime "org.liquibase:liquibase-core:${liquibaseVersion}"
    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate6:${liquibaseHibernateVersion}"
    liquibaseRuntime "org.postgresql:postgresql:${postgresVersion}"
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime "org.apache.commons:commons-lang3:3.14.0"
    liquibaseRuntime sourceSets.main.output
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath
}

tasks.withType(org.liquibase.gradle.LiquibaseTask).configureEach {
    dependsOn 'classes'
}

// Liquibase plugin configuration
liquibase {
    activities {
        main {
            changelogFile diffChangelogFile
            propertyFile 'src/main/resources/liquibase.properties'
            author "${changesetAuthor}"
        }
    }
    runList = 'main'
}

task('dbChangeLog') {
  doFirst(){
    liquibase {
      activities {
        main {
          changelogFile "src/main/resources/db/changelog/db.changelog-master.yaml"
        }
      }
    }
  }
}

update.dependsOn('dbChangeLog')
